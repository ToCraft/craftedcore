buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.johnrengelman:shadow:8.+"
    }
}

apply plugin: com.github.jengelman.gradle.plugins.shadow.ShadowPlugin

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader}"
    modApi "net.fabricmc.fabric-api:fabric-api:${parent.props.fabric}+${parent.name}"

    implementation project(path: ":$parent.name:fabric", configuration: "namedElements")
    common(project(path: ":$parent.name:common", configuration: "namedElements")) { transitive false }
    common(project(path: ":$parent.name:testmod-common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":$parent.name:testmod-common", configuration: "transformProductionFabric")) { transitive = false }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("testmod-fabric-dev-shadow")
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set("testmod-fabric")
}
